plugins {
	id 'java'
	id 'maven'
    id 'maven-publish'
    //see https://github.com/researchgate/gradle-release
    id 'net.researchgate.release' version '2.6.0'
}

group = 'com.github.plast-lab'

sourceCompatibility = 1.8

wrapper {
    gradleVersion = "5.4.1"
}

repositories {
    mavenCentral()
    maven {
        url "http://centauri.di.uoa.gr:8081/artifactory/plast-public"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile "org.clyze:clue-common:3.11.+"
    // https://mvnrepository.com/artifact/com.offbytwo/docopt
    compile group: 'com.offbytwo', name: 'docopt', version: '0.6.0.20150202'
    compile "ext:hprof-parser:1.0-SNAPSHOT"
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

task fatjar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'heapdl.main.Main',
                   'Implementation-Version': version
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

if (project.hasProperty('artifactory_user')) {

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                credentials {
                    username artifactory_user
                    password artifactory_password
                }
                url "$artifactory_contextUrl/libs-public-release-local"
            }
        }
    }

    release {
        failOnSnapshotDependencies = false
        failOnUnversionedFiles = false
        failOnCommitNeeded = false

        git {
           commitVersionFileOnly = true
        }
        /* publish before releasing
           buildTasks =['build', 'publish']
         */
    }

    afterReleaseBuild.dependsOn publish
}
