plugins {
    id 'java'
    id 'application'
    id 'maven'
    id 'maven-publish'
    //see https://github.com/researchgate/gradle-release
    id 'net.researchgate.release' version '2.6.0'
    id 'org.mikeneck.graalvm-native-image' version 'v0.8.0'
    id "com.jfrog.bintray" version "1.8.4"
}

group = 'org.clyze'

sourceCompatibility = 1.8

mainClassName = 'heapdl.main.Main'

wrapper {
    gradleVersion = "6.2"
}

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/gfour/plast-lab' }
    maven { url 'https://jitpack.io' }
    // maven { url "http://centauri.di.uoa.gr:8081/artifactory/plast-public" }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile "org.clyze:clue-common:3.22.5"
    // https://mvnrepository.com/artifact/com.offbytwo/docopt
    compile group: 'com.offbytwo', name: 'docopt', version: '0.6.0.20150202'
    // compile "ext:hprof-parser:1.0"
    implementation 'com.github.eaftan:hprof-parser:3668fd734f79c2b949f0703654dfda812c800707'
    runtime "com.google.guava:guava:27.1-jre" // needed by hprof-parser
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

task fatjar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName,
                   'Implementation-Version': version
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

if (project.hasProperty('artifactory_user')) {

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact tasks.sourcesJar
                artifact tasks.javadocJar
            }
        }

        repositories {
            maven {
                credentials {
                    username artifactory_user
                    password artifactory_password
                }
                url "$artifactory_contextUrl/libs-public-release-local"
            }
        }
    }

    release {
        failOnSnapshotDependencies = false
        failOnUnversionedFiles = false
        failOnCommitNeeded = false

        git {
           commitVersionFileOnly = true
        }
        /* publish before releasing
           buildTasks =['build', 'publish']
         */
    }

    afterReleaseBuild.dependsOn publish
}

nativeImage {
    graalVmHome = System.getenv('GRAALVM_HOME')
    mainClass = mainClassName
    executableName = 'heapdl'
    outputDirectory = file("$buildDir/bin")
    arguments(
	'--allow-incomplete-classpath',
	'--report-unsupported-elements-at-runtime',
	'--no-fallback',
	'--no-server',
	'--enable-all-security-services',
	'--report-unsupported-elements-at-runtime'
    )
}

// Upload to bintray with:
//   ./gradlew clean build bintrayUpload --info
// Needs properties: bintray_user, bintray_key.
if (project.hasProperty('bintray_user') && project.hasProperty('bintray_key')) {
    bintray {
        user = bintray_user
        key = bintray_key
        publications = ['mavenJava']
        configurations = ['archives']
        pkg {
            repo = 'plast-lab'
            name = 'HeapDL'
            desc = 'The HeapDL tool.'
            licenses = ['MIT']
            labels = ['java']
            vcsUrl = 'https://github.com/plast-lab/HeapDL'
            version {
                name = project.version
                released = new Date()
                vcsTag = project.version
            }
        }
    }
}
