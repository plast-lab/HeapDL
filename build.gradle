plugins {
	id 'java'
	id 'maven'
    id 'maven-publish'
    //see https://github.com/researchgate/gradle-release
    id 'net.researchgate.release' version '2.4.0'
}

group = 'com.github.plast-lab'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "http://centauri.di.uoa.gr:8081/artifactory/plast-public"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile "org.apache.ivy:ivy:2.3.0",
            "log4j:log4j:1.2.14",                              // Logging implementation
            "commons-io:commons-io:2.4"                        // File Utils
    // https://mvnrepository.com/artifact/com.offbytwo/docopt
    compile group: 'com.offbytwo', name: 'docopt', version: '0.6.0.20150202'
    compile(group: "sun", name: "jhat", version: "PATCHED")
}

if (project.hasProperty('artifactory_user')) {

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                credentials {
                    username artifactory_user
                    password artifactory_password
                }
                url "$artifactory_contextUrl/libs-public-release-local"
            }
        }
    }

    release {
        failOnSnapshotDependencies = false
        failOnUnversionedFiles = false
        failOnCommitNeeded = false

        git {
           commitVersionFileOnly = true
        }
        /* publish before releasing
           buildTasks =['build', 'publish']
         */
    }

    afterReleaseBuild.dependsOn publish
}
