buildscript {
    repositories {
        mavenCentral()
        maven { url="https://jitpack.io" }
        maven { url="https://repo.grails.org/grails/core/" }
    }
}

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    //see https://github.com/researchgate/gradle-release
    id 'net.researchgate.release' version '3.1.0'
    id 'org.mikeneck.graalvm-native-image' version '1.4.1'
    id "com.jfrog.bintray" version "1.8.5"
}

group = 'org.clyze'

application {
    mainClass = 'heapdl.main.Main'
}

wrapper {
    gradleVersion = '9.3.1'
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}
gradle.rootProject.ext.groovyVersion='5.0.4'
gradle.rootProject.ext.clueCommonVersion='3.26.2'

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.1.0-M1'

    implementation "org.apache.groovy:groovy:${groovyVersion}"                  // Groovy
    implementation "org.apache.groovy:groovy-ant:${groovyVersion}"
    implementation "org.apache.groovy:groovy-cli-commons:${groovyVersion}"      // Command line processor (Groovy wrapper)
    implementation "com.offbytwo:docopt:0.6.0.20150202"
    implementation "com.github.eaftan:hprof-parser:849678cca7"
    implementation "com.github.plast-lab:clue-common:${clueCommonVersion}"

    runtimeOnly "com.google.guava:guava:33.5.0-jre" // needed by hprof-parser
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

task fatJar(type: Jar) {
    archiveClassifier = 'all'

    manifest {
        attributes 'Main-Class': application.mainClass,
                   'Implementation-Version': version
    }
    // avoid “duplicate entry” failures when jars contain the same paths
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output

    // stuff dependencies inside the jar
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath
        .findAll { it.name.endsWith('jar') }
        .collect { zipTree(it) }
    }

    // common: strip signature metadata that becomes invalid after repackaging
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

tasks.named("assemble") {
    dependsOn(sourcesJar, javadocJar)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

nativeImage {
    graalVmHome = System.getenv('GRAALVM_HOME')
    mainClass = application.mainClass
    executableName = 'heapdl'
    outputDirectory = file("$buildDir/bin")
    arguments(
        '--allow-incomplete-classpath',
        '--report-unsupported-elements-at-runtime',
        '--no-fallback',
        '--no-server',
        '--enable-all-security-services',
        '--report-unsupported-elements-at-runtime'
    )
}